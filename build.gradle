allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'maven'

  version = '0.0.1'

  sourceCompatibility = 11
  targetCompatibility = 11

  group = "kz.greetgo.cm"

}

subprojects {
  repositories {
    mavenLocal()
    if (System.getenv('GG_REPO') != null && System.getenv('GG_REPO').length() > 0) maven { url System.getenv('GG_REPO') }
    mavenCentral()
  }

  dependencies {
    ext.testNgVersion = '6.14.3'

    compile 'org.slf4j:slf4j-api:1.7.22'

    testCompile "org.testng:testng:$testNgVersion"
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
  }

  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
  }

  test.useTestNG()

  idea.module.downloadSources = true
  idea.module.downloadJavadoc = true
  idea.module.inheritOutputDirs = false
  idea.module.outputDir = file("$buildDir/classes/main/")
  idea.module.testOutputDir = file("$buildDir/classes/test/")

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  [compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.test.java.srcDirs = ["test_src"]
  sourceSets.main.resources.srcDirs = ["src_resources"]
  sourceSets.test.resources.srcDirs = ["test_resources"]

  sourceSets.main.resources {
    srcDirs += sourceSets.main.java.srcDirs
    exclude '**/*.java'
  }
  sourceSets.test.resources {
    srcDirs += sourceSets.test.java.srcDirs
    exclude '**/*.java'
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name
      attributes 'Implementation-Version': archiveVersion
      attributes 'Implementation-Vendor': "greetgo!"
      attributes 'Created-By': System.getProperty("user.name")
      attributes 'Created-At': new Date().format("yyyy-MM-dd/HH:mm:ss")
    }
  }
}
